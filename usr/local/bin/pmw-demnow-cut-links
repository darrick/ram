#!/bin/zsh

##############################################################################
##
## demnow-cut-links
##
## Called from a scheduled macro cart (set up using rdcatch) to create
## Linux hard links from Cut001 to cuts 002 and 003 in the
## DemocracyNow carts.
##
## See also: rdcatch(1) and the setup for Democracy Now in that
## application.
##
##############################################################################

setopt REMATCH_PCRE
setopt NO_CASE_MATCH
zmodload zsh/regex
zmodload zsh/datetime

# Get zsh functions necessary for this script
[[ -r ${ROOT:-/}usr/local/bin/zsh-functions ]] && source ${ROOT:-/}usr/local/bin/zsh-functions

# log STDOUT and STDERR of this script and all commands called by this script to separate files
exec 1> /var/tmp/${0##*/}.out
exec 2> /var/tmp/${0##*/}.err

RM=$(whence rm) ; RM=${RM:-${ROOT:-/}bin/rm}
LN=$(whence ln) ; LN=${LN:-${ROOT:-/}bin/ln}
LOGGER=$(whence logger) ; LOGGER=${LOGGER:-${ROOT:-/}usr/bin/logger}

sndDir=${SND_DIR:-${ROOT:-/}var/snd}

# Get the current weeday name in full (%A).
today=${TODAY:-$(strftime "%A" ${EPOCHSECONDS})}

# Get the Democracy Now cart number for today. BUG ALERT: proper
# operation of this depends on Democracy Now carts being titled with
# the string "Democray Now <Weekday-name>" in the cart TITLE.
typeset -Z 6 cartNumber=$(doSQL "select NUMBER from CART where TITLE like 'Democracy Now%${today}%'")

if [[ "${cartNumber}" =~ '\d+' ]] ; then

    if [[ -f ${sndDir}/0${cartNumber}_001.wav ]] ; then

	${RM} -f ${sndDir}/${cartNumber}_00[23].wav
	${LN} ${sndDir}/${cartNumber}_001.wav ${sndDir}/${cartNumber}_002.wav
	${LN} ${sndDir}/${cartNumber}_001.wav ${sndDir}/${cartNumber}_003.wav

	if doSQL "update CUTS set PLAY_COUNTER = 0, LOCAL_COUNTER = 0 where CART_NUMBER = ${cartNumber}" ; then
            ${LOGGER} -t ${0##*/} -p local7.info -i "INFO: reset play counter for cuts in cart ${cartNumber}."

	    if doSQL "update CUTS set LAST_PLAY_DATETIME = NULL where CART_NUMBER = ${cartNumber}" ; then
		${LOGGER} -t ${0##*/} -p local7.info -i "INFO: reset last played date for all cuts in cart ${cartNumber}."
	    else
		${LOGGER} --stderr -t ${0##*/} -p local7.err -i "ERROR: Could reset last played date for cuts in cart ${cartNumber} (${?})."
	    fi
	else
            ${LOGGER} --stderr -t ${0##*/} -p local7.err -i "ERROR: Could reset play counter for cuts in cart ${cartNumber} (${?})."
	fi
    else
	${LOGGER} --stderr -t ${0##*/} -p local7.err -i "ERROR: Could not find audio file ${sndDir}/0${cartNumber}_001.wav (${?})."
    fi

else
    ${LOGGER} --stderr -t ${0##*/} -p local7.err -i "ERROR: Unable to locate cart number for Democracy Now '${today}' (${?})."
fi

exit
