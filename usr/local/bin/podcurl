#!/bin/zsh

zmodload zsh/regex

# The URL must be passed on the command line.
URL=${1:?"Need a URL to get."}

# The location for downloaded files, must match the folder named as
# the Rivendell dropbox.
downloadFolder=${DOWNLOAD_FOLDER:-${HOME}/Downloads/podcasts}

# Parse the URL into its separate components.
# protocol is everything before the "://" separator.
protocol=${URL%%://*}

# The hostname comes after "://" and before the (optional) ":\d+/"
# (port specifier) and trailing pathname.
host=${${${URL##*://}%%/*}%%:*}

# The filename is just the last component of the path in the URL.
file=${URL##*/}

# Snag the email ("producer") and domain ("organization") from the URL
# if it looks like it is from AudioPort.
if [[ "${host}" =~ "audioport" ]] ; then
    producer=${${URL##*/audioport_files/}%%@*}
    organization=${${URL##*@}%%/*}
    downloadFolder=${downloadFolder}/audioport/${producer}/${organization}
else
    downloadFolder=${downloadFolder}/${host}
fi

# Make sure the folder exists before attempting the download.
if [[ ! -d ${downloadFolder} ]] ; then
    mkdir -p ${downloadFolder}
fi

if [[ -d ${downloadFolder} ]] ; then
    # Download the file if we can find the curl(1) executable app.
    if [[ -x $(which curl) ]] ; then
	cd "${downloadFolder}"
	curl --silent --remote-name "${URL}"
	exitValue=${?}
    else
	echo "Cannot find the 'curl' app. Cannot continue"
	exitValue=3
    fi
else
    echo "Cannot find ${downloadFolder}, I give up."
    exitValue=2
fi

exit ${exitValue}
